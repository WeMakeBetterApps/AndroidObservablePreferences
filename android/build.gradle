apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.6

repositories {
    mavenLocal()
    mavenCentral()
}

configurations { providedCompile }
sourceSets.main.compileClasspath += configurations.providedCompile
sourceSets.test.compileClasspath += configurations.providedCompile
sourceSets.test.runtimeClasspath += configurations.providedCompile

dependencies {
    compile project(':core')
    providedCompile 'com.google.android:android:4.1.1.4'
    providedCompile 'io.reactivex:rxjava:1.1.0'
    providedCompile 'com.google.code.gson:gson:2.3.1'
    testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'ObservablePreferences Android'
                packaging 'jar'
                // optionally artifactId can be defined here
                artifactId = "observablepreferences-android"
                description 'A set of observable persistent preferences for Android'
                url 'https://github.com/WeMakeBetterApps/ObservablePreferences'

                scm {
                    connection 'scm:git:http://github.com/wemakebetterapps/observablepreferences'
                    developerConnection 'scm:git:https://github.com/wemakebetterapps/observablepreferences'
                    url 'http://github.com/wemakebetterapps/observablepreferences'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'spierce7'
                        name 'Scott Pierce'
                        email 'scott@wemakebetterapps.com'
                    }
                }
            }
        }
    }
}